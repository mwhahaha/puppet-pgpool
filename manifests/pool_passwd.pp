# == Type: pgpool::pool_passwd
#
# This resource allows for the manipulation of users and password hashes within
# a pool_passwd file via file_line.
#
# === Parameters
#
# [*ensure*]
#   String. This controls if the setting should be <tt>present</tt> or
#   <tt>absent</tt> in the configuration file.
#   Defaults to <tt>present</tt>.
#
# [*target*]
#   String. This is the file that we will be working with.
#   Defaults to the pool_passwd_file from the pgpool::config class.
#
# [*password_hash*]
#   String. This should be the password hash as generated by pg_md5.
#   Defaults to <tt>undef</tt>.
#
# === Variables
#
# [*name*]
#   String. This is the name of the user to manage.
#
# [*pool_passwd_file*]
#   String. This is the pool_passwd file and is pulled from pgpool::config.
#
# === Examples
#
# pgpool::config::val { 'myuser':
#   target        => '/etc/pgpool/my_pool_passwd',
#   password_hash => 'md53fc18e3994ebb693e804e5c112461531'
# }
#
# === Authors
#
# Alex Schultz <aschultz@next-development.com>
#
# === Copyright
#
# Copyright 2015 Alex Schultz, unless otherwise noted.
#
define pgpool::pool_passwd (
  $ensure        = present,
  $target        = undef,
  $password_hash = undef,
) {
  $target_real = $target ? {
    undef   => $::pgpool::config::pool_passwd_file,
    default => $target
  }

  $ensure_real = $password_hash ? {
    undef   => absent,
    default => $ensure,
  }

  file_line { $name:
    ensure => $ensure_real,
    path   => $target_real,
    line   => "${name}:${password_hash}",
    match  => "^${name}:",
    before => Service['pgpool']
  }
}
